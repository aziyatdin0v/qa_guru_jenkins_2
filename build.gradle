plugins {
    id 'java'
    id 'io.qameta.allure' version '2.10.0'
}

def allureVersion = "2.19.0",
    slf4jVersion = "2.0.3",
    selenideVersion = "6.9.0",
    jUnitVersion = "5.9.0"

repositories {
    mavenCentral()
}

compileTestJava {
    options.encoding = 'UTF-8'
}

// конфигурация аллюра
allure {
    report {
        version.set(allureVersion)
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set(allureVersion) //версия интеграции фреймворка и Allure
            }
        }
    }
}

dependencies {
    testImplementation(
            "io.qameta.allure:allure-selenide:$allureVersion",
            "org.junit.jupiter:junit-jupiter:$jUnitVersion",
            "com.codeborne:selenide:$selenideVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "com.github.javafaker:javafaker:1.0.2"
    )
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task test_form(type: Test) {
    useJUnitPlatform {
        includeTags "test_form"
    }
}

task test_form_with_minimum_data(type: Test) {
    useJUnitPlatform {
        includeTags "test_form_with_minimum_data"
    }
}
